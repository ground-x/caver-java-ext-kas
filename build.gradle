buildscript {
    ext.caver_version = '1.5.6-rc.6'
}

plugins {
    id 'java'

    id 'maven-publish'
    id 'signing'
    id 'idea'

    id "com.jfrog.bintray" version "1.8.5"
}

allprojects {
    version '1.1.1'
    group 'xyz.groundx.caver'
    description 'An extension library of caver-java for using KAS (Klaytn API Service).'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'


    repositories {
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    // Deploy
    task javadocJar(type: Jar) {
        archiveClassifier = "javadoc"
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = "sources"
        from sourceSets.main.allSource
    }

    ext {
        bintrayUser = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        bintrayKey = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_KEY')
        bintrayGpgPassphrase = project.hasProperty('bintrayGpgPassphrase') ? project.property('bintrayGpgPassphrase') : System.getenv('BINTRAY_GPG_PASSPHRASE')

        isAndroidBuild = project.version.endsWith("-android")
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = project.name
                    description = project.description
                    version = project.version
                    url = "https://www.klaytnapi.com"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "repo"
                        }
                    }
                    developers {
                        developer {
                            id = "groundxDev"
                            name = "caver-java-ext-kas Authors"
                            email = "developer@groundx.xyz"
                        }
                    }
                    scm {
                        connection = "scm:git:https://*.git"
                        developerConnection = "scm:git://*.git"
                        url = "https://*.git"
                    }
                }
            }
        }
    }

    bintray {
        user = bintrayUser
        key = bintrayKey
        publications = ['mavenJava']
        publish = true
        pkg {
            desc = project.description
            repo = 'maven'
            name = 'caver-java-ext-kas'
            licenses = ['Apache-2.0']
            issueTrackerUrl = 'https://github.com/ground-x/caver-java-ext-kas/issues'
            vcsUrl = 'https://github.com/ground-x/caver-java-ext-kas.git'
            websiteUrl = 'https://www.klaytnapi.com'
            publicDownloadNumbers = true

            version {
                name = project.version
                desc = project.description
                gpg {
                    sign = true
                    passphrase = bintrayGpgPassphrase
                }
                mavenCentralSync {
                    sync = false
                }
            }
        }
    }

    task release {
        dependsOn 'build'
        dependsOn 'bintrayUpload'

        doLast {
            if (!bintrayUser || !bintrayKey || !bintrayGpgPassphrase) {
                throw new InvalidUserDataException("Required parameters missing:  'bintrayUser', 'bintrayKey', 'bintrayGpgPassphrase'")
            }

            logger.lifecycle(" - bintrayUser={}", bintrayUser)
            logger.lifecycle(" - bintrayKey={}", bintrayKey ? "provided" : "not_provided")
            logger.lifecycle(" - bintrayGpgPassphrase={}", bintrayGpgPassphrase ? "provided" : "not_provided")
        }

        tasks.findByName('bintrayUpload').mustRunAfter 'build'
    }



    dependencies {
        if(isAndroidBuild) {
            compile "com.klaytn.caver:core:$caver_version-android"
        } else {
            compile "com.klaytn.caver:core:$caver_version"
        }


        compile 'io.swagger.core.v3:swagger-annotations:2.0.0'
        compile 'com.squareup.okhttp:okhttp:2.7.5'
        compile 'com.squareup.okhttp:logging-interceptor:2.7.5'
        compile 'com.google.code.gson:gson:2.8.1'
        compile 'io.gsonfire:gson-fire:1.8.0'
        compile 'org.threeten:threetenbp:1.3.5'

        compile 'io.github.cdimascio:dotenv-java:2.2.0'


        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile 'org.mockito:mockito-core:3.6.28'
        testCompile "ch.qos.logback:logback-core:1.2.3",
                "ch.qos.logback:logback-classic:1.2.3"
    }
}
